plugins {
    id 'java'
    id 'idea'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
    id 'war'
}

group 'org.radarcns'
version '1.0-SNAPSHOT'

war {
    baseName = 'redcap'
    version = project.version
    extension = 'war'
    archiveName = baseName + '.' + extension
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

configurations {
    codacy
    provided
    compile.extendsFrom provided
}

repositories {
    mavenCentral()
    maven { url 'http://packages.confluent.io/maven/' }
    maven { url 'https://oss.jfrog.org/artifactory/libs-snapshot/' }
    maven { url 'https://dl.bintray.com/radar-cns/org.radarcns' }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output + test.compileClasspath
            runtimeClasspath += main.output + test.output + test.compileClasspath
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}


ext.apacheCommonsIoVersion = '2.5'
ext.apacheCommonsLangVersion = '3.6'
ext.jerseyVersion = '2.26'
ext.jerseymediaVersion = '2.24'
ext.junitVersion = '4.12'
ext.logbackVersion = '1.1.7'
ext.okhttp3Version = '3.8.1'
ext.radarCommonsVersion = '0.9.0'
ext.radarOauthClientVersion = '0.4.0'
ext.tomcatVersion = '8.0.44'
ext.grizzlyVersion = '2.3.19'

dependencies {
    providedCompile group: 'org.apache.tomcat', name: 'tomcat-catalina', version: tomcatVersion

    runtimeOnly group: 'ch.qos.logback', name:'logback-classic', version: logbackVersion
    runtimeOnly group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: jerseymediaVersion

    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: jerseyVersion
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion

    compile group: 'org.glassfish.grizzly', name: 'grizzly-http-server', version: grizzlyVersion
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version: jerseyVersion

    compile group: 'org.radarcns', name: 'radar-commons-server', version: radarCommonsVersion
    compile group: 'org.radarcns', name: 'oauth-client-util', version: radarOauthClientVersion

    compile group: 'commons-io', name: 'commons-io', version: apacheCommonsIoVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsLangVersion

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttp3Version

    testCompile group: 'junit', name: 'junit', version: junitVersion
}

//---------------------------------------------------------------------------//
// Style checking                                                            //
//---------------------------------------------------------------------------//
checkstyle {
    // codacy version
    toolVersion '6.16'
    ignoreFailures false

    // ignore tests
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
}

pmd {
    // pmd version
    toolVersion = '5.5.2'
    ignoreFailures = false

    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]

    consoleOutput = true

    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

pmdTest {
    ruleSetFiles = files("config/pmd/test_ruleset.xml")
}

//---------------------------------------------------------------------------//
// Testing                                                                   //
//---------------------------------------------------------------------------//
tasks.matching {it instanceof Test}.all {
    def stdout = new LinkedList<String>()
    beforeTest { TestDescriptor td ->
        stdout.clear()
    }

    onOutput { TestDescriptor td, TestOutputEvent toe ->
        stdout.addAll(toe.getMessage().split('(?m)$'))
        while (stdout.size() > 100) {
            stdout.remove()
        }
    }

    afterTest { TestDescriptor td, TestResult tr ->
        if (tr.resultType == TestResult.ResultType.FAILURE) {
            println()
            print("${td.className}.${td.name} FAILED")
            if (stdout.empty) {
                println(" without any output")
            } else {
                println(" with last 100 lines of output:")
                println('=' * 100)
                stdout.each { print(it) }
                println('=' * 100)
            }
        }
    }

    testLogging {
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat "full"
    }
}

test {
    testLogging {
        // Show that tests are run in the command-line output
        events "skipped", "failed"
    }
}

task downloadApplicationDependencies {
    description "Pre-downloads application dependencies"
    configurations.compileClasspath.files
    configurations.runtimeClasspath.files
}

//---------------------------------------------------------------------------//
// Build system metadata                                                     //
//---------------------------------------------------------------------------//
idea {
    module {
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}


task integrationTest(type: Test, dependsOn: ['installMP', 'copyConf']) {
    description = "Run integration tests (located in src/integrationTest/...)."

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    // This is not needed, but I like to see which tests have run
    testLogging {
        events "skipped", "failed", "passed"
    }
}

integrationTest.finalizedBy 'removeLock'

ext.sudoLinux = System.properties['os.name'].toLowerCase().contains('linux') ? ['sudo'] : []
ext.lock = 'src/integration-test/.RUNNING_INSTANCE_LOCK'

task installMP(type: Exec) {
    doFirst{
        def lockFile = new File(lock)
        if (lockFile.exists()) {
            throw new GradleException("A previous instance of the management portal is still running. Please stop it first and then try again.")
        } else {
            lockFile.createNewFile()
        }
    }

    workingDir 'src/integration-test/'
    standardInput = System.in
    commandLine sudoLinux + ['docker-compose', 'up', '-d', '--build', '--force-recreate']

    doLast {
        // wait until the hotstorage and managementportal are ready
        sleep(60_000)
    }
}

task removeLock(type: Exec) {
    new File(lock).delete()

    workingDir 'src/integration-test/'
    commandLine sudoLinux + ['docker-compose', 'down']
}

task copyConf(type: Copy) {
    from "src/integration-test/resources/radar.yml"
    into "/usr/local/tomcat/conf/radar/"

    filter { String line ->
        line.replace("management_portal_url: http://managementportal-app:8090/",
                "management_portal_url: http://localhost:8090/")
    }
}